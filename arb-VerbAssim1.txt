read regex 
! معالجة المضارع
! الثلاثي
! الصحيح المهموز
! الأفعال الثلاثية المبدوءة بالهمزة في المضارع يتم إدغامها مع ألف المتكلم
! مثال أكل آكل، أمر آمر
أ -> آ || "+pres" أ "^ss^" _ Alphabet^2 "^se^" [\"^dbl" | "+token" .#.] .o. 
أ -> 0 || "+pres" _ "^ss^" آ Alphabet^2 "^se^" [\"^dbl" | "+token" .#.] .o. 

! الأفعال الثلاثية التي يتوسطها همزة على نبرة في المضارع يتم قلبها إلى همزة على ألف مثال سئم يسأم، يئس ييأس
ئ -> أ || "+pres" ("+pass") [(أ)|ن|ي|ت] "^ss^" Alphabet _ Alphabet "^se^".o. 

! الأفعال الثلاثية التي تبدأ ب آ يتم وضع الهمزة إما على واو مثل آل يؤول أو على ياء مثل آن يئين
أ -> ئ || "+pres" [(أ)|ن|ي|ت] "^ss^" _ ا Alphabet "^se^" "^orig" ي .o. 
أ -> ؤ || "+pres" [(أ)|ن|ي|ت] "^ss^" _ ا Alphabet "^se^" "^orig" و .o.

! الأفعال الثلاثية التي تنتهي بهمزة على ياء في المضارع يتم قلبها إلى همزة على ألف مثال صدئ يصدأ، برئ يبرأ
ئ -> أ || "+pres" [(أ)|ن|ي|ت] "^ss^" Alphabet^2 _ "^se^".o. 

! الهمزة على الألف أو الياء في آخر الكلمة إذا تلتها واو أو ألف فإنها تكتب على السطر مثال برئ يبرءون ويبرءان وقرأ يقرءون ويقرءان وتكتب الهمزة على نبرة في غير ذلك مثل وطأ يطئون
! قاعدة عامة
! [ئ|أ] -> ء || "+pres" ("+pass") [(أ)|ن|ي|ت] "^ss^" Alphabet [د|ذ|ر|ز|و] _ "^se^" [ا|و].o.
! [ئ|أ] -> ئ || "+pres" ("+pass") [(أ)|ن|ي|ت] "^ss^" Alphabet \[د|ذ|ر|ز|و] _ "^se^" [ا|و].o.

! الأفعال الرباعية والخماسية والسداسية التي بها همزة على ألف قبل الآخر تقلب همزة على نبرة مثل طمأن يطمئن واطمأن يطمئن
! فيما عدا الأفعال المبتدئة بتاء المطاوعة مثل ترأس يترأس
أ -> ئ || "+pres" ("+pass") [(أ)|ن|ي|ت] "^ss^" [Alphabet-ت] Alphabet^{1,3} _ Alphabet "^se^" .o.

! >>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>
! معالجة المبني للمفعول
! الأفعال الثلاثية المبدوءة بهمزة على ألف في المضارع المبني للمفعول يتم تحويلها إلى همزة على واو
! مثال أكل يؤكل، أمر يؤمر
أ -> ؤ || "+pres" "+pass" [(أ)|ن|ي|ت] "^ss^" _ Alphabet^2 "^se^" [\"^dbl" | "+token" .#.] .o. 

! بالباقي مثل المبني للفاعل
! >>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>
! المعتل المثال
! الأفعال الثلاثية المبدوءة بالواو في المضارع يتم حذف الواو منها
! مثال وزن يزن، وقف يقف
و -> 0 || "+pres" ([(أ)|ن|ي|ت]) "^ss^" _ Alphabet^2 "^se^" [\"^dbl" | "+token" .#.] .o.

! >>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>
! المعتل الأجوف
! الأفعال الثلاثية التي يتوسطها ألف في المضارع يتم رد الألف إلى أصلها إما واو مثل قال يقول أو ياء مثل باع يبيع أو ألف مثل نام ينام
! ويجوز حذفها
ا (->) 0 || "+pres" [(أ)|ن|ي|ت] "^ss^" [Alphabet|Alphabet^2|Alphabet^4] _ Alphabet "^se^" .o. 
ا -> ي || "+pres" [(أ)|ن|ي|ت] "^ss^" Alphabet _ Alphabet "^se^" "^orig" ي .o. 
ا -> و || "+pres" [(أ)|ن|ي|ت] "^ss^" Alphabet _ Alphabet "^se^" "^orig" و .o.

! يحذف حرف العلة المتوسط إذا أضيف إلى نون النسوة مثال يعود يعدن وتعدن يقوم يقمن وتقمن
 [و|ي|ا] -> 0 || "+pres" ("+pass") [ي|ت] "^ss^" Alphabet _ Alphabet "^se^" ("^orig" Alphabet "^orig") ن "+fem" .o.

! وعندئذ إذا كان الحرف الأخير همزة فإنها تقلب إلى همزة على نبرة مثل جاء يجئن
ء -> ئ || "+pres" ([ي|ت]) "^ss^" Alphabet _ "^se^" ("^orig" Alphabet "^orig") ن "+fem" .o.

! >>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>
! المعتل الناقص
! الأفعال الثلاثية التي تنتهي بألف (ا أو ى) في المضارع يتم رد الألف إلى أصلها إما واو مثل غزا يغزو ونمى ينمو أو ياء مثل رمى يرمي 
[ا|ى] -> ي || "+pres" [(أ)|ن|ي|ت] "^ss^" Alphabet^2 _ "^se^" "^orig" ي "^orig" [\[و|ي]| \"^dbl" | "+token" .#.].o. 
[ا|ى] -> و || "+pres" [(أ)|ن|ي|ت] "^ss^" Alphabet^2 _ "^se^" "^orig" و "^orig"  [\[و|ي]| \"^dbl" | "+token" .#.] .o. 

! إذا كانت الألف الأخيرة غير منقلبة عن أصل فإنها تقلب ألف مقصورة مثل رقا يرقى
ا -> ى || "+pres" [(أ)|ن|ي|ت] "^ss^" Alphabet^2 _ "^se^" "+token" .#. .o. 

! معالجة رأى
أ -> 0 || "+pres" ("+pass") [(أ)|ن|ي|ت] "^ss^" ر _ ى "^se^" ("^orig" Alphabet "^orig").o. 

! إذا كان الفعل منتهيا بياء فإنها تنقلب إلى ألف مثل قوي يقوى ورضي يرضى إذا لم يلحقها شيء
! وتأخذ نفس نفس تصريفات الألف اللينة مع نون النسوة وألف الإثنين إلخ
ي -> ى || "+pres" ("+pass") [(أ)|ن|ي|ت] "^ss^" Alphabet^2 _ "^se^" .o. 

! الأفعال الثلاثية التي تنتهي بألف لينة ى تقلب إلى ياء ي إذا أضيفت إلى ألف الاثنين أو نون النسوة مثل نأى ينأيان ينأين 
! وكذلك الرباعي والخماسي والسداسي مثل استجدى يستجديان
ى -> ي || "+pres" ("+pass") [(أ)|ن|ي|ت] "^ss^" Alphabet^{1,5} _ "^se^" ("^dbl" [2|3|4|5|6] "^dbl") ("^orig" Alphabet "^orig") [ا|ن] .o. 

!  وتحذف الياء أو الواو أو الألف إذا تلتها واو الجماعة أو ياء المؤنثة المخاطبة مثل قوي يقوون تقوين و رضي يرضون ترضين ورمى يرمون ترمين وغزا يغزون تغزين و نأى ينأون تنأين وكذلك الرباعي والخماسي والسداسي مثل استجدى يستجدون وتستجدي
! ؟نون النسوة
! here it undergoes two assimilations
! moved to general rules
! [ى|ي|ا|و] -> 0 || "+pres" ("+pass") ([ي|ت]) "^ss^" Alphabet^{1,5} _ "^se^" ("^dbl" [2|3|4|5|6] "^dbl") ("^orig" Alphabet "^orig") [و|ي].o.

! تحذف حروف العلة من آخر الفعل جوازا أي في حالة الجزم عند إضافتها إلى ضمائر المفعول مثل سمى يسمها دعى يدعهم ونفى ينفه
[ى|ي|ا|و] (->) 0 || "+pres" Alphabet "^ss^" Alphabet^{1,5} _ "^se^" ("^dbl" [2|3|4|5|6] "^dbl") ("^orig" Alphabet "^orig") "+token" Alphabet .o.

!  تقلب الألف المقصورة ياء في غير ذلك مثل نوى ينوي ينويان واستجدى يستجدي يستجديان
! إلا إذا كانت مبدوءة بالتاء مثل تبنى وتحدى
ى -> ي || "+pres" Alphabet "^ss^" [Alphabet-ت] Alphabet^{2,4} _ "^se^" .o.
ى -> ي || "+pres" Alphabet "^ss^" [Alphabet-ت] Alphabet _ "^se^" "^dbl" 2 "^dbl" .o.

! في الثلاثي تقلب الألف المقصورة ياء إذا كان أصلها ياء مثل رمى يرمي
ى -> ي || "+pres" Alphabet "^ss^" Alphabet^{1,2} _ "^se^" "^orig" ي .o.
! وتقلب واوا إذا كان أصلها واو مثل نمى ينمو وبدى يبدو
ى -> و || "+pres" Alphabet "^ss^" Alphabet^{1,2} _ "^se^" "^orig" و .o.
! وتقلب ألفا إذا كان أصلها ألفا وتلاها أي شيى (فيما عدا الألف والواو والياء حيث تم معالجتها في قواعد سابقة) مثل رأى يراه ورعى يرعاه
ى -> ا || "+pres" Alphabet "^ss^" Alphabet^{1,2} _ "^se^" ("^orig" ا "^orig") "+token" Alphabet .o.
! وكذلك الرباعي المبدوء بتاء المطاوعة مثل تحدى تحداه وتبنى تبناه
ى -> ا || "+pres" Alphabet "^ss^" ت Alphabet^{1,2} _ "^se^" ("^dbl" [2|3|4|5|6] "^dbl") "+token" Alphabet .o.

! معالجة المبني للمفعول
! إذا كان الفعل منتهيا بألف فإنها تنقلب مقصورة إلى مثل دعا يدعى وشكا يشكى
ا -> ى || "+pres" "+pass" [(أ)|ن|ي|ت] "^ss^" Alphabet^2 _ "^se^" .o. 

! >>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>
! الرباعي في المضارع
! الأفعال الرباعية التي يكون فيها ألف قبل الآخر يقلب ياء مثل أقال يقيل وأباد يبيد وكذلك السداسي مثل استعان يستعين
ا -> ي || "+pres" [(أ)|ن|ي|ت] "^ss^" [Alphabet^2|Alphabet^4] _ Alphabet "^se^" .o. 

! إذا كان مضعف الثاني تقلب الألف همزة مثل أمر يؤمر وأجر يؤجر
! التحديد بأن الألف يبتعها حرف أو اثنين لأن حرف العلة المتطرف قد يحذف
أ -> ؤ || "+pres" ("+pass") [(أ)|ن|ي|ت] "^ss^" _ Alphabet^{1,2} "^se^" "^dbl" 2 .o. 

! الأفعال الرباعية التي تنتهي بألف لينة تقلب ياء مثل أوحى يوحي وكذلك الخماسي مثل انتهى ينتهي والسداسي مثل استجدى يستجدي ولا ينطبق هذا على المضعف الخماسي مثل تولى يتولى
ى -> ي || "+pres" [(أ)|ن|ي|ت] "^ss^" [Alphabet-ت] Alphabet^{4,5} _ "^se^" [\"^dbl" | "+token" .#.] .o. 
! ى -> ي || "+pres" [(أ)|ن|ي|ت] "^ss^" Alphabet^{2,4} _ "^se^" "^dbl" .o. 

! الأفعال الرباعية التي تنتهي بهمزة على ألف أو همزة على السطر تقلب همزة على ياء مثل أرجأ يرجئ وكذلك الخماسي والسداسي
! وذلك في غير المبدوء بالتاء مثل تواطأ يتواطأ وتجرأ يتجرأ
[ء|أ] -> ئ || "+pres" [(أ)|ن|ي|ت] "^ss^" [Alphabet-ت] Alphabet^{1,4} [Alphabet-ا] _ "^se^" [\"^dbl" | "+token" .#.] .o. 
أ -> ئ || "+pres" [(أ)|ن|ي|ت] "^ss^" [Alphabet-ت] Alphabet^{1,3} _ "^se^" "^dbl" .o. 

! وكذلك الرباعي الذي ينتهي بألف مثل أحيا يحيي
ا -> ي || "+pres" Alphabet "^ss^" [Alphabet-ت] Alphabet^2 _ "^se^" .o.

! الأفعال الرباعية التي تنتهي بألف لينة تقلب ياء في المبني للمفعول إذا أضيفت إليها أي شيء وكذلك الخماسي والسداسي مثل استجدى يستجديا
ى -> ي || "+pres" ("+pass") [(أ)|ن|ي|ت] "^ss^" Alphabet^{3,5} _ "^se^" ("^orig" Alphabet "^orig") Alphabet .o. 
ى -> ي || "+pres" ("+pass") [(أ)|ن|ي|ت] "^ss^" Alphabet^{2,4} _ "^se^" "^dbl" [2|3|4|5|6] "^dbl" Alphabet .o. 

! الأفعال الرباعية التي تبدأ ب أا يتم قلب الألف الثانية واوا مثل آتى يؤتي وآثر يؤثر
ا -> ؤ || "+pres" ("+pass") [(أ)|ن|ي|ت] "^ss^" أ _ Alphabet^2 "^se^" .o. 

! الأفعال الرباعية التي تبدأ بهمزة في المضارع يتم حذف الهمزة مثل أكرم يكرم وأقال يقيل وأتم يتم 
أ -> 0 || "+pres" ("+pass") [(أ)|ن|ي|ت] "^ss^" _ Alphabet^3 "^se^" .o. 
أ -> 0 || "+pres" ("+pass") [(أ)|ن|ي|ت] "^ss^" _ Alphabet^2 "^se^" "^dbl" 3 .o. 

! الأفعال الخماسية التي تبدأ بألف وصل في المضارع يتم حذف الألف مثل ابتكر يبتكر وامتد يمتد وكذلك السداسي مثل استخرج يستخرج واستقر يستقر
ا -> 0 || "+pres" ("+pass") [(أ)|ن|ي|ت] "^ss^" _ Alphabet^{2,5} "^se^" .o. 
ا -> 0 || "+pres" ("+pass") [(أ)|ن|ي|ت] "^ss^" _ Alphabet^{1,4} "^se^" "^dbl" .o. 

! الأفعال التي تنتهي بحرف علة يحذف جوازا في المضارع مثل أدى يؤد، دعا يدع، تعدى يتعد، استجدى يستجد
[ا|و|ي|ى] (->) 0 || "+pres" ("+pass") [(أ)|ن|ي|ت] "^ss^" Alphabet^{1,5} _ "^se^" ("^dbl" [2|3|4|5|6] "^dbl") ("^orig" Alphabet "^orig") "+token" .#. .o. 

! الأفعال التي يكون قبل آخرها ألف يجوز حذفها مثل قال يقول يقل، باع يبيع يبع، أشار يشير يشر، استفاد يستفيد يستفد
! [ا|ي] (->) 0 || "+pres" ("+pass") [(أ)|ن|ي|ت] "^ss^" Alphabet _ [Alphabet-ء] "^se^" [\[ا | و | ي] | "+token" .#.] .o.
! [ا|ي] (->) 0 || "+pres" ("+pass") [(أ)|ن|ي|ت] "^ss^" أ Alphabet _ [Alphabet-ء] "^se^" [\[ا | و | ي] | "+token" .#.] .o.
! [ا|ي] (->) 0 || "+pres" ("+pass") [(أ)|ن|ي|ت] "^ss^" ا س ت Alphabet _ [Alphabet-ء] "^se^" [\[ا | و | ي] | "+token" .#.];
[ا|ي] (->) 0 || "+pres" [(أ)|ن|ي|ت] "^ss^" Alphabet _ [Alphabet-ء] "^se^" [\[ا | و | ي] | "+token" .#.] .o.
! [ا|ي] (->) 0 || "+pres" ("+pass") [(أ)|ن|ي|ت] "^ss^" أ Alphabet _ [Alphabet-ء] "^se^" [\[ا | و | ي] | "+token" .#.] .o.
[ا|ي] (->) 0 || "+pres" [(أ)|ن|ي|ت] "^ss^" ا س ت Alphabet _ [Alphabet-ء] "^se^" [\[ا | و | ي] | "+token" .#.];

